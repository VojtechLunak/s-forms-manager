SELECT ?hash
WHERE
{
    {
        SELECT (group_concat (?newOstr) as ?originConcatStr)
        {
            SELECT ?newOstr
            WHERE
            {
                GRAPH ?contextUri {
                    ?s <http://onto.fel.cvut.cz/ontologies/form/has-question-origin> ?o .
                    ?s <http://onto.fel.cvut.cz/ontologies/documentation/has_answer> ?a .
                        OPTIONAL {?a <http://onto.fel.cvut.cz/ontologies/documentation/has_object_value> ?v1}  # object value, data value?
                        OPTIONAL {?a <http://onto.fel.cvut.cz/ontologies/documentation/has_data_value> ?v2}
                    BIND (str(?o) as ?oStr)
                    BIND (str(?v1) as ?v1Str)
                    BIND(concat(?oStr, COALESCE(?v2,?v1Str)) as ?newOstr)
                }
            }
            ORDER BY ?oStr
        }
    }
    BIND(md5(?originConcatStr)  as ?hash)
}